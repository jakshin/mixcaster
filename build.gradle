//file:noinspection GroovyAssignabilityCheck

plugins {
    id 'java'
    id 'distribution'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'com.apollographql.apollo' version '2.5.10'
    id 'org.barfuin.gradle.taskinfo' version '1.3.0'  // For the `tiTree <task>` task

    // The Snyk plugin defines two tasks: snyk-test, snyk-monitor
    // https://snyk.io/blog/gradle-plugin-by-snyk-gradle-dependencies-scanning/
    id 'io.snyk.gradle.plugin.snykplugin' version '0.4'
}

repositories {
    mavenCentral()
}

dependencies {
    // testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    // testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'junit:junit:4.13.2'

    implementation 'com.apollographql.apollo:apollo-runtime:2.5.10'
}

def appVersion = 'dev'  // Must be either "dev" or major.minor.patch

// Enable some javac diagnostics
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

// Configure/tweak tasks
distTar.enabled(false)

distributions {
    main {
        distributionBaseName = "$project.name-$appVersion"
        contents {
            from shadowJar.archiveFile
            from 'config'
            from 'README.md'
        }
    }
}

installDist {
    // Install for real, on the machine the build is running on
    destinationDir = new File("${System.getenv("HOME")}/Applications/Mixcaster")

    doLast {
        def settingsFile = new File(destinationDir.getPath() + '/mixcaster-settings.properties')
        def newSettings = "http_port = 6498\n" +
                        "log_dir = ~/Library/Logs/Mixcaster-dist\n"
        settingsFile.text = newSettings

        exec {
            // If our launchd agent already exists, it's removed before installing the new one
            commandLine "${System.getenv("HOME")}/Applications/Mixcaster/mixcaster", "-install"
        }
    }
}

jar {
    manifest {
        attributes (
                'Main-Class': 'jakshin.mixcaster.Main',
                'Implementation-Version': appVersion
        )
    }
}

shadowJar {
    archiveFileName = "${project.name.toLowerCase()}.jar"

    // Should be able to enable this again with shadow 7.1+
    // See https://github.com/johnrengelman/shadow/issues/679
    // minimize()
}

snyk {
    // Also need to set the $SNYK_TOKEN environment variable
    severity = 'low'
    autoDownload = true
    autoUpdate = true
}

test {
    useJUnit()
}

// Use "shadowJar" in place of "jar"
assemble.dependsOn(shadowJar)
distTar.dependsOn(shadowJar)
distZip.dependsOn(shadowJar)
installDist.dependsOn(shadowJar)
jar.enabled(false)
